buildscript {
    ext {
        springBootVersion = "2.4.3"
    }
}

plugins {
    id "org.unbroken-dome.test-sets" version "${testSetsPluginVersion}"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.bmuschko.docker-remote-api" version "6.7.0"
    id "com.avast.gradle.docker-compose" version "0.14.0"
    id "com.moowork.node" version "1.3.1"
    id "com.github.michaelruocco.gradle-postman-runner" version "0.1.2"
}

ext {
    karateVersion = "0.9.6"
    logbackJsonVersion = "0.1.5"

    imageName = "michaelruocco/${project.name}"
    isSnapshot = "${version}".contains("SNAPSHOT")
}

testSets {
    integrationTest
}

configurations {
    runtimeOnly.exclude module: "slf4j-simple"
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}

dependencies {
    implementation project(":plain-app")
    implementation project(":use-cases")
    implementation project(":config")
    implementation project(":passcode-generator")
    implementation project(":in-memory-repository")
    implementation project(":mongo-repository")
    implementation project(":delivery-config")
    implementation project(":json")
    implementation testFixtures(project(":context-verification-loader"))
    implementation "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.hateoas:spring-hateoas"
    implementation "com.github.michaelruocco:spring-filters:0.1.9"
    implementation "com.github.michaelruocco:json-adapter:${jsonAdapterVersion}"
    implementation "com.github.michaelruocco:testing-clocks:${testingClocksVersion}"
    implementation "com.github.michaelruocco:random-value-suppliers:${randomValueSuppliersVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "org.mongodb:mongo-java-driver:${mongoVersion}"
    runtimeOnly "ch.qos.logback.contrib:logback-json-classic:${logbackJsonVersion}"
    runtimeOnly "ch.qos.logback.contrib:logback-jackson:${logbackJsonVersion}"
    runtimeOnly "net.logstash.logback:logstash-logback-encoder:6.6"
    implementation("com.github.dalet-oss:mongobee:1.0.4") {
        exclude module: "mongo-java-driver"
    }

    testImplementation testFixtures(project(":entities"))

    integrationTestImplementation "org.junit-pioneer:junit-pioneer:${junitPioneerVersion}"
    integrationTestImplementation "com.intuit.karate:karate-core:${karateVersion}"
    integrationTestImplementation "com.intuit.karate:karate-junit5:${karateVersion}"
    integrationTestImplementation "org.awaitility:awaitility"
    integrationTestImplementation "org.testcontainers:junit-jupiter:1.15.2"
    integrationTestImplementation "junit:junit"
    integrationTestRuntimeOnly "com.intuit.karate:karate-apache:${karateVersion}"
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperties = System.getProperties()
    systemProperty "spring.profiles.active", System.properties.getOrDefault("spring.profiles.active", "stubbed,simple-logging")
    systemProperty "server.port", System.properties.getOrDefault("server.port", "8082")
    systemProperty "request.logging.enabled", System.properties.getOrDefault("request.logging.enabled", "true")
    systemProperty "response.logging.enabled", System.properties.getOrDefault("response.logging.enabled", "true")
    systemProperty "response.filtering.enabled", System.properties.getOrDefault("response.filtering.enabled", "true")
}

docker {
    registryCredentials {
        username.set(System.getenv("DOCKER_USERNAME"))
        password.set(System.getenv("DOCKER_PASSWORD"))
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task buildImage(type: DockerBuildImage) {
    inputDir.set(file("."))
    images.add("${imageName}:${version}")
    if (!isSnapshot) {
        images.add("${imageName}:latest")
    }
    buildArgs = ["VERSION": version]
}

task pushImage(type: DockerPushImage) {
    images.set(buildImage.images)
}

dockerCompose {
    startedServices = ['idv-one-time-passcode-app']
    environment.put 'APP_VERSION', version
}

postman {
    collections = fileTree(dir: "postman", include: "*_collection.json")
    environment = file("postman/idv-local.postman_environment.json")
    stopOnError = true
}

apply from: rootProject.file("publish.gradle")